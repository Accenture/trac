/*
 * Copyright 2020 Accenture Global Solutions Limited
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "java"
    id "idea"
}

apply from: "${rootProject.projectDir.path}/gradle/base-java.gradle"


sourceSets {

    test.resources {
        srcDir 'src/test/resources'
        srcDir 'src/schema'
    }
}

dependencies {

    implementation project(':trac-api-metadata')
    implementation project(':trac-api-services')
    implementation project(':trac-lib-common')
    implementation project(':trac-lib-db')

    // CLI library for processing startup args (part of the .config package)
    implementation group: 'commons-cli', name: 'commons-cli', version: "$commons_cli_version"

    // Logging
    implementation group: 'org.slf4j', name: 'slf4j-api', version: "$slf4j_version"
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: "$log4j_version"
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: "$log4j_version"
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: "$log4j_version"

    // Test

    // Migration support for JUnit 4 Rules, provided by gRPC for managing test service instances
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-migrationsupport', version: "$junit_version"

    testImplementation (group: 'io.grpc', name: 'grpc-testing', version: "$grpc_version") {
        // Do not pull in JUnit 4, use migration support from JUnit 5 instead
        exclude group: 'junit', module: 'junit'
    }

    testImplementation project(':sql-drivers')

    // Embedded databases to test some dialects locally
    testImplementation group: 'ch.vorburger.mariaDB4j', name: 'mariaDB4j', version: "$mariadb_embedded_version"
}


// When running from an IDE, add plugin dependencies to the classpath
// TODO: This will need updating so plugin projects don't need adding individually!

gradle.startParameter.taskNames.forEach { task ->

    if (task.endsWith("main()")) {
        dependencies {
            runtimeOnly project(':sql-drivers')
        }
    }
}
