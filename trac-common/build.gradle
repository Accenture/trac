
plugins {
    id "com.google.protobuf" version "0.8.10"
    id "idea"
}

apply from: "${rootProject.projectDir.path}/gradle/base-java.gradle"


sourceSets {
    main {
        java {
            // Include generated code from protoc / gRPC
            srcDirs "${project.buildDir}/generated-sources/main/java"
            srcDirs "${project.buildDir}/generated-sources/main/grpc"
        }
    }
}


dependencies {

    // Protoc generates code using the 'Generated' annotation from this package
    compile group: 'javax.annotation', name: 'javax.annotation-api', version: "$javax_annotation_version"

    // Core protobuf for Java
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: "$proto_version"

    // GRPC for Protobuf / Netty
    compile group: 'io.grpc', name: 'grpc-protobuf', version: "$grpc_version"
    compile group: 'io.grpc', name: 'grpc-netty', version: "$grpc_version"
    compile group: 'io.grpc', name: 'grpc-stub', version: "$grpc_version"

    // Google APIs framework for REST APIs
    compile group: 'com.google.api.grpc', name: 'proto-google-common-protos', version: "$gapi_version"
}


protobuf {

    protoc {
        artifact = "com.google.protobuf:protoc:$proto_version"
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpc_version}"
        }
    }

    generateProtoTasks.generatedFilesBaseDir = "${project.buildDir}/generated-sources"

    generateProtoTasks {
        all().each { task ->

        task.builtins {
            java { }
        }

        task.plugins {
            grpc { }
        }
    }}
}
